// Copyright (c) 2003-2024 Broad Institute, Inc., Massachusetts Institute of Technology, and Regents 
// of the University of California. All rights reserved.
apply plugin: 'java'
import org.apache.tools.ant.filters.ReplaceTokens

ext.moduleName = 'org.gsea_msigdb.gsea'
ext.mainClassName = 'xapps.gsea.GSEA'

repositories {
    flatDir { dirs 'modules' }
}

dependencies {
    implementation fileTree(dir: 'modules', include: '*.jar')
}

tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:deprecation'
    options.deprecation = true
}

tasks.withType(Copy).configureEach {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

processResources {
    // Set the current timestamp for release builds; pass in a blank property to enable (i.e. -Ptimestamp="" at CLI)
    if (timestamp.equals("")) { timestamp = new Date().format("EEE, d MMM yyyy HH:mm:ss Z") }

    filesMatching('edu/mit/broad/genome/resources/build.properties') {
        filter (ReplaceTokens,
                tokens: [ VERSION: version, VERSION_MAJOR: versionMajor, VERSION_MINOR: versionMinor, VERSION_PATCH: versionPatch,
                          BUILD: buildNumber, TIMESTAMP: timestamp ])
    }
}

jar {
    archiveFileName = "gsea-minimal-${version}.jar"
    manifest {
        attributes(
            "Permissions": "all-permissions",
            "Application-Name": "GSEA",
            "Built-By": System.getProperty('user.name'),
            "GSEA-Build-Date": timestamp,
            "Main-Class": mainClassName,
        )
    }
}

compileJava {
    options.release = 17
    inputs.property("moduleName", moduleName)
    doFirst {
        options.compilerArgs = [
            '--module-path', classpath.asPath
        ]
        classpath = files()
    }
}

task createDist(type: Copy, dependsOn: jar)  {
    from ("src/main/resources/edu/mit/broad/genome/resources/icon_64x64.png")
    from ('scripts') {
        include 'gsea*'
        include 'logging.properties'
        include 'readme.txt'
        exclude 'GSEA_mac_*'
        exclude 'mac.app'
        exclude 'nsis'
    }
    with copySpec {
        from ("${buildDir}/libs")
        from ("modules")
        into "modules"
    }
    into "${buildDir}/GSEA-dist"
}

task createDistZip(type: Zip, dependsOn: createDist) {
    archiveBaseName = "GSEA"
    archiveFileName = "GSEA_${version}-NoJava.zip"
    destinationDirectory = file("${buildDir}/distZip")
    from "${buildDir}/GSEA-dist"
    into { "GSEA_${version}" }

    doLast {
        project.exec { commandLine('chmod', '775', createDistZip.archivePath ) }
    }
}

task createLinuxDistZip(type: Zip, dependsOn: createDist) {
    doFirst {
        if (jdkBundleLinux == "") {
            throw new GradleException("Required property not set: jdkBundleLinux");
        }
    }
    archiveBaseName = "GSEA_Linux"
    archiveFileName = "GSEA_LinuxIntel_${version}-WithJava.zip"
    destinationDirectory = file("${buildDir}/distZip")
    with copySpec { from jdkBundleLinux into "jdk" }
    from ("${buildDir}/GSEA-dist") {
        exclude "*.bat"
        exclude "*.command"
    }
    into { "GSEA_Linux_${version}" }
}

task createMacAppDist(type: Copy, dependsOn: createDist) {
    with copySpec {
        from ("scripts/mac.app")
        into "GSEA_${version}.app"
    }
    with copySpec {
        from ("scripts/Info.plist.template")
        filter (ReplaceTokens, tokens: [ VERSION: version ])
        rename "Info.plist.template", "Info.plist"
        into "GSEA_${version}.app/Contents"
    }
    with copySpec {
        from ("src/main/resources/edu/mit/broad/genome/resources/icon_64x64.png")
        into "GSEA_${version}.app/Contents/Resources"
    }
    with copySpec {
        from ("scripts/logging.properties")
        into "GSEA_${version}.app/Contents/Resources"
    }
    with copySpec {
        from ("${buildDir}/GSEA-dist/modules") include '*.jar'
        into "GSEA_${version}.app/Contents/Java/modules"
    }
    into "${buildDir}/GSEA-MacApp-dist"

    doLast {
        project.exec { commandLine('chmod', '775', "${buildDir}/GSEA-MacApp-dist/GSEA_${version}.app" ) }
    }
}

task createMacAppDistZip(type: Zip, dependsOn: createMacAppDist) {
    archiveFileName = "GSEA_MacApp_${version}_NoJava.app.zip"
    destinationDirectory = file("${buildDir}/distZip")
    with copySpec {
        from ("scripts/GSEA_mac_universal")
        rename "GSEA_mac_universal", "GSEA"
        into "GSEA_${version}.app/Contents/MacOS"
    }
    from "${buildDir}/GSEA-MacApp-dist"
}

task createMacAppWithJavaDistZip(type: Zip, dependsOn: createMacAppDist) {
    doFirst {
        if (jdkBundleMac == "") {
            throw new GradleException("Required property not set: jdkBundleMac");
        }
    }
    archiveFileName = "GSEA_MacApp_${version}_WithJava.app.zip"
    destinationDirectory = file("${buildDir}/distZip")
    with copySpec { from jdkBundleMac into "GSEA_${version}.app/Contents/jdk" }
    with copySpec {
        from ("scripts/GSEA_mac_arm")
        rename "GSEA_mac_arm", "GSEA"
        into "GSEA_${version}.app/Contents/MacOS"
    }
    from "${buildDir}/GSEA-MacApp-dist"
}

task createMacAppIntelWithJavaDistZip(type: Zip, dependsOn: createMacAppDist) {
    doFirst {
        if (jdkBundleMacIntel == "") {
            throw new GradleException("Required property not set: jdkBundleMacIntel");
        }
    }
    archiveFileName = "GSEA_MacAppIntel_${version}_WithJava.app.zip"
    destinationDirectory = file("${buildDir}/distZip")
    with copySpec { from jdkBundleMacIntel into "GSEA_${version}.app/Contents/jdk" }
    with copySpec {
        from ("scripts/GSEA_mac_x64")
        rename "GSEA_mac_x64", "GSEA"
        into "GSEA_${version}.app/Contents/MacOS"
    }
    from "${buildDir}/GSEA-MacApp-dist"
}

task createWinDist(type: Copy, dependsOn: createDist) {
    with copySpec {
        from("${buildDir}/GSEA-dist") {
            exclude "*.sh"
            exclude "*.command"
        }
        from ("scripts/nsis") { include "GSEA_Icon.ico" }
        into "GSEA_${version}"
    }
    with copySpec {
        // Create a build-specific NSIS installer script.
        from ("scripts/nsis/installer.nsi")
        filter (ReplaceTokens, tokens: [ VERSION: version, WITH_JAVA: "-NoJava" ])
    }
    from ("scripts/nsis") { include "LICENSE_WIN.txt" }
    into "${buildDir}/GSEA-WinExe-dist"
}

task createWinWithJavaDist(type: Copy, dependsOn: createWinDist) {
    doFirst {
        if (jdkBundleWindows == "") {
            throw new GradleException("Required property not set: jdkBundleWindows");
        }
    }
    with copySpec {
        from("${buildDir}/GSEA-dist") {
            exclude "*.sh"
            exclude "*.command"
        }
        from ("scripts/nsis") { include "GSEA_Icon.ico" }
        into "GSEA_${version}"
    }
    with copySpec {
        // Create a build-specific NSIS installer script.
        from ("scripts/nsis/installer.nsi")
        filter (ReplaceTokens, tokens: [ VERSION: version, WITH_JAVA: "-WithJava" ])
    }
    with copySpec { from jdkBundleWindows into "GSEA_${version}/jdk" }
    from ("scripts/nsis") { include "LICENSE_WIN.txt" }
    into "${buildDir}/GSEA-WinExe-WithJava-dist"
}

task createWinExeDist(type: Exec, dependsOn: createWinDist) {
    doFirst {
        if (makensisCommand == "") {
            throw new GradleException("Required property not set: makensisCommand");
        }
    }
    commandLine(makensisCommand, "-O${buildDir}/tmp/nsis-build.log",
                "${buildDir}/GSEA-WinExe-dist/installer.nsi")
}

task createWinExeWithJavaDist(type: Exec, dependsOn: createWinWithJavaDist) {
    doFirst {
        if (makensisCommand == "") {
            throw new GradleException("Required property not set: makensisCommand");
        }
    }
    commandLine(makensisCommand, "-O${buildDir}/tmp/nsis-build.log",
                "${buildDir}/GSEA-WinExe-WithJava-dist/installer.nsi")
}

task signWinExeDist(type: Exec, dependsOn: createWinExeDist) {
    doFirst {
        if (signcodeCommand == "") {
            throw new GradleException("Required property not set: signcodeCommand");
        }
    }
    standardInput = new ByteArrayInputStream( keyPassword.getBytes() );
    commandLine(signcodeCommand, "-spc", spcFile, "-v", pvkFile, "-a", "sha512", 
                "-\$", "commercial", "-n", "GSEA ${version}", "-i", "http://www.gsea-msigdb.org/",
                "-t", tsaurl, "-tr", "10", "-tw", "15", "${buildDir}/GSEA-WinExe-dist/GSEA_Win_${version}-NoJava-installer.exe")
}

task signWinExeWithJavaDist(type: Exec, dependsOn: createWinExeWithJavaDist) {
    doFirst {
        if (signcodeCommand == "") {
            throw new GradleException("Required property not set: signcodeCommand");
        }
    }
    standardInput = new ByteArrayInputStream( keyPassword.getBytes() );
    commandLine(signcodeCommand, "-spc", spcFile, "-v", pvkFile, "-a", "sha512", 
                "-\$", "commercial", "-n", "GSEA ${version}", "-i", "http://www.gsea-msigdb.org/",
                "-t", tsaurl, "-tr", "10", "-tw", "15",  "${buildDir}/GSEA-WinExe-WithJava-dist/GSEA_Win_${version}-WithJava-installer.exe")
}
